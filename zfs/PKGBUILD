# Maintainer: Steven Armstrong <steven-aur at armstrong.cc>

pkgname='zfs'
pkgver=0.6.1
pkgrel=1
pkgdesc='Kernel module and support files for the Zettabyte File System.'
arch=('i686' 'x86_64')
url='http://zfsonlinux.org'
license=('CDDL')
makedepends=('linux-headers')
depends=('spl')
optdepends=('dkms: automatic kernel module rebuilding'
            'linux-headers: automatic kernel module rebuilding'
            'linux-lts-headers: automatic kernel module rebuilding')
provides=("$pkgname-modules")
install="$pkgname.install"
source=("http://archive.zfsonlinux.org/downloads/zfsonlinux/$pkgname/$pkgname-$pkgver.tar.gz"
      "$pkgname.bash-completion"
      "$pkgname.initcpio.install"
      "$pkgname.initcpio.hook"
      "$pkgname.service")
md5sums=('822cd73c139d89369d6c3944f8afe659'
         '3e1c4a29c4f7d590e6a3041f2c61d6ff'
         'e3b91b9dbcca7be58249d7cd1eba2aeb'
         '37067e0970143e5f89c86af0a25e4408'
         '161e6a5f5f314c9308b4a4565c01fe45')

build() {
   cd "$srcdir/$pkgname-$pkgver"
   ./autogen.sh
   ./configure --prefix=/usr \
               --sysconfdir=/etc \
               --sbindir=/usr/sbin \
               --libdir=/usr/lib \
               --datadir=/usr/share \
               --includedir=/usr/include \
               --with-udevdir=/lib/udev \
               --libexecdir="/usr/lib/$pkgname-$pkgver" \
               --with-config=user
   #FIXME: where to get /etc/default/zfs whithout having to maintain it myself?
   ./scripts/dkms.mkconf -n $pkgname -v $pkgver -c /etc/default/$pkgname -f dkms.conf
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   make DESTDIR=$pkgdir install

   # remove uneeded files
   rm -r $pkgdir/etc/init.d
   rm -r $pkgdir/usr/lib/dracut

   # move module tree /lib -> /usr/lib
   cp -r $pkgdir/{lib,usr}
   rm -r $pkgdir/lib

   install -D -m 644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
   install -D -m 644 "$srcdir/$pkgname.bash-completion" "$pkgdir/usr/share/bash-completion/completions/$pkgname"

   # cleanup source tree, then copy it to /usr/src for later use by dkms
   make distclean > /dev/null
   install -d -m 755 "$pkgdir/usr/src"
   cp -r "$srcdir/$pkgname-$pkgver" "$pkgdir/usr/src/$pkgname-$pkgver"

   # install the mkinitcpio hook to compile the modules
   cd "$srcdir"
   sed -i "s|_arch|$CARCH|" "$pkgname.initcpio.install"
   install -d -m 755 "$pkgdir/usr/lib/initcpio/install"
   install -m 644 "$pkgname.initcpio.install" "$pkgdir/usr/lib/initcpio/install/$pkgname"
   install -d -m 755 "$pkgdir/usr/lib/initcpio/hooks"
   install -m 644 "$pkgname.initcpio.hook" "$pkgdir/usr/lib/initcpio/hooks/$pkgname"
}
